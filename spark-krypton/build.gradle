import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'org.jetbrains.kotlin.kapt' version '1.8.10'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    maven { url 'https://repo.kryptonmc.org/releases' }
    maven { url 'https://repo.kryptonmc.org/snapshots' }
}

dependencies {
    implementation project(':spark-common')

    compileOnly 'org.kryptonmc:krypton-api:5769c45cee'
    kapt 'org.kryptonmc:krypton-annotation-processor:5769c45cee'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
}

processResources {
    filter(ReplaceTokens, tokens: ['version': project.pluginVersion, 'desc': project.pluginDescription])
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-krypton.jar"

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
