plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'org.jetbrains.kotlin.kapt' version '1.5.21'
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven { url 'https://repo.kryptonmc.org/releases' }
}

dependencies {
    implementation project(':spark-common')

    compileOnly 'org.kryptonmc:api:0.37.1'
    kapt 'org.kryptonmc:api:0.37.1'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '16'
        freeCompilerArgs += ['-Xjvm-default=enable']
    }
}

// Overwritten from the root project since Krypton requires Java 16
compileJava {
    options.compilerArgs.clear()
    options.compilerArgs.addAll(['--release', '16'])
}

blossom {
    replaceTokenIn('src/main/kotlin/me/lucko/spark/krypton/KryptonSparkPlugin.kt')
    replaceToken '@version@', project.pluginVersion
    replaceToken '@desc@', project.pluginDescription
}

shadowJar {
    archiveFileName = 'spark-krypton.jar'

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
